---
layout: post
title: "使用ESP-01实现单总线多DS18x20温度采集"
date: 2018-02-06 20:12:00 +0800
categories: [esp8266]
tags: [esp,esp8266,esp-01,DS18B20,温度,采集,mqtt]
comments: true
---


基于安信可（AI-Thinker）ESP-01实现单总线多DS18x20的温度采集，并通过mqtt进么通信。


功能
============

用ESP01作为主控，定时获取到总线上的多个（建议最多8个）ds18b20的温度数据，后通过mqtt上传到服务器(暂时iot.eclipse.org凑合一用)。

硬件
============

1 ESP-01(S)模块(建议去安信可（AI-Thinker）官方淘宝店买)

2 DS18B20(淘宝4元左右一个)

3 面包板、杜邦线若干、R4.7k、USB转TTL

硬件接线
============

1 测试接线(用了两个DS18B20测试)

![ESP-01(S) 开发连接图]({{ site.baseurl}}/assets/images/esp-01_ds18b20_dev.jpg){:width="300"}

![ESP-01(S) 开发连接图]({{ site.baseurl}}/assets/images/esp-01_ds18b20_dev1.jpg){:width="300"}

2 应用场景( 4个DS18B20 )

![ESP-01(S) 开发连接图]({{ site.baseurl}}/assets/images/esp-01_ds18b20.jpg){:width="300"}


软件
============

1 Arduino IDE

2 Libs:

* ESP8266WiFi
* ArduinoJson
* PubSubClient
* OneWire
* DallasTemperature

代码
============


```arduino
/**
   基于ESP-01的单总线多DS18x20温度采集
   原件：esp8266,ds19b20,R4.7k
   Libs:ESP8266WiFi,ArduinoJson,PubSubClient,OneWire,DallasTemperature
   薛海峰
*/

#include <ESP8266WiFi.h>
#include <ArduinoJson.h>
#include <PubSubClient.h>
#include <OneWire.h>
#include <DallasTemperature.h>

//Wifi 相关参数
const char* wifi_ssid = "your_ssid";
const char* wifi_pass = "your_pass";
WiFiClient espClient;

//Mqtt 相关参数
const char* mqtt_server = "iot.eclipse.org";
const char* mqtt_cid = "cli-180806";
const char* mqtt_usr = "";
const char* mqtt_pwd = "";
PubSubClient mqtt(espClient);

//DS18b20 相关参数
#define ONE_WIRE_BUS 2              //数据总线占用 d2
#define TEMPERATURE_PRECISION 12    //温度精度
OneWire oneWire(ONE_WIRE_BUS);
DallasTemperature sensors(&oneWire);

int numDevices;                     //记录设备数量
DeviceAddress tDeviceAddress;       //记录设备地址

int interval = 10 * 60 * 1000;            //发送频次,10分钟(基本，不严格)
long lastTime = 0;

void client_doit1(void) {
  Serial.println("client_doit1");
  sensors.requestTemperatures(); // 获取温度数据
  delay(10);
  DynamicJsonBuffer tJsonBuffer(256);
  JsonObject& root = tJsonBuffer.createObject();
  root["cid"] = mqtt_cid;
  root["sensor"] = "temperature";
  JsonArray& data = root.createNestedArray("data");
  for (int i = 0; i < numDevices; i++)  {
    JsonObject& item = data.createNestedObject();
    if (sensors.getAddress(tDeviceAddress, i)) {
      // Device Address
      String tdaStr = "";
      for (uint8_t i = 0; i < 8; i++) {
        char str[8]; sprintf(str, "%X", tDeviceAddress[i]); tdaStr += str;
      }
      item["did"] = tdaStr;
      item["value"] = sensors.getTempC(tDeviceAddress);
      item["desc"] = "degree Fahrenheit";
    }
    //else unknow device! Check your power requirements and cabling
  }
  Serial.println("Publish Massage:");
  String output;
  root.printTo(output);
  //root.printTo(Serial);
  root.prettyPrintTo(Serial);
  bool pub = mqtt.publish("upload", output.c_str());
  Serial.print("\n\rRC="); Serial.println(pub);
}

void client_doit2(void) {
  Serial.println("client_doit2");
  sensors.requestTemperatures(); // 获取温度数据
  delay(10);
  for (int i = 0; i < numDevices; i++)  {
    DynamicJsonBuffer tJsonBuffer(256);
    JsonObject& root = tJsonBuffer.createObject();
    root["cid"] = mqtt_cid;
    root["sensor"] = "temperature";
    if (sensors.getAddress(tDeviceAddress, i)) {
      // Device Address
      String tdaStr = "";
      for (uint8_t i = 0; i < 8; i++) {
        char str[8]; sprintf(str, "%X", tDeviceAddress[i]); tdaStr += str;
      }
      root["did"] = tdaStr;
      root["value"] = sensors.getTempC(tDeviceAddress);
      root["desc"] = "degree Fahrenheit";
      Serial.println("Publish Massage:");
      String output;
      root.printTo(output);
      //root.printTo(Serial);
      root.prettyPrintTo(Serial);
      bool pub = mqtt.publish("upload", output.c_str());
      Serial.print("\n\rRC="); Serial.println(pub);
    }
    //else unknow device! 
  }
}

void mqtt_callback(char* topic, byte* payload, unsigned int length) {
  Serial.println("mqtt_callback");
  String msgStr = "";
  for (int i = 0; i < length; i++)  msgStr += (char)payload[i];
  DynamicJsonBuffer msgBuffer;
  JsonObject& msgJson = msgBuffer.parseObject(msgStr);
  if (!msgJson.success()) {
    Serial.println("msgJson parseObject() failed in mqtt_callback.");
    String rMsg = (String)"{\"cid\":\"" + (String)mqtt_cid + (String)"\",\"data\":\"msgJson parseObject() failed\"" + msgStr + (String)"}";
    mqtt.publish("error", rMsg.c_str());
    return;
  }
  int cmd = msgJson["cmd"];
  switch (cmd) {
    case 0: {
        Serial.println("Ping Client.");
        msgJson["cid"] = mqtt_cid;
        String output;
        msgJson.printTo(output);
        mqtt.publish(msgJson["data"], output.c_str());
        break;
      }
    case 1: {
        Serial.println("To Subscribe:");
        for (int i = 0; i < msgJson["data"].size(); i++)  mqtt.subscribe(msgJson["data"][i]);
        break;
      }
    case 2: {
        Serial.println("To Unsubscribe:");
        for (int i = 0; i < msgJson["data"].size(); i++)  mqtt.unsubscribe(msgJson["data"][i]);
        break;
      }
    case 3: {
        Serial.print("Change interval: ");
        interval = (int)msgJson["data"] * 1000;
        Serial.println((int)msgJson["data"]);
        break;
      }
    default: {
        Serial.println("unknow cmd:");
        break;
      }
  }
  Serial.println("Message:");
  msgJson.prettyPrintTo(Serial);
  Serial.println(" ");
}

void wifi_setup() {
  delay(10);
  Serial.printf("\n\rConnecting to %s\n\r", wifi_ssid);
  WiFi.begin(wifi_ssid, wifi_pass);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.printf("\n\rWiFi [%s] connected\n\r", wifi_ssid);
  Serial.print("MAC address: ");
  Serial.println(WiFi.macAddress());
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());
  Serial.println("----------  ----------  ----------  ----------");
}

void mqtt_setup() {
  //mqtt.setClient (espClient);
  mqtt.setServer(mqtt_server, 1883);
  mqtt.setCallback(mqtt_callback);
}

void mqtt_reconnect() {
  // 循环，直到重新连接
  while (!mqtt.connected()) {
    Serial.print("Attempting MQTT connection...");
    // 尝试连接
    if (mqtt.connect(mqtt_cid)) {
      //mqtt.connect(mqtt_cid, mqtt_usr, mqtt_pwd);
      Serial.printf("connected \n\rClientId: %s\n\r", mqtt_cid);
      //订阅一个消息
      mqtt.subscribe(mqtt_cid);
      // 一旦连接，发布一个上线消息
      char msgStr[32];
      sprintf(msgStr, "{\"cid\":\"%s\"}", mqtt_cid);
      mqtt.publish("online", msgStr);
    } else {
      Serial.printf("failed, rc=%d try again in 5 seconds\n\r", mqtt.state());
      // 等待x毫秒后重试
      delay(5000);
    }
  }
}

void client_setup() {
  sensors.begin();
  numDevices = sensors.getDeviceCount();  // 获取设备个数
  Serial.print("Found "); Serial.print(numDevices, DEC); Serial.println(" devices.");
  for (int i = 0; i < numDevices; i++)  {
    if (sensors.getAddress(tDeviceAddress, i))    {
      // set the resolution to 9 bit (Each Dallas/Maxim device is capable of several different resolutions)
      sensors.setResolution(tDeviceAddress, TEMPERATURE_PRECISION);
    } else {
      Serial.print("Found device at ");
      Serial.print(i, DEC);
      Serial.println(" but could not detect address. Check power and cabling");
    }
  }
}

void setup() {
  Serial.begin(115200);
  wifi_setup();
  mqtt_setup();
  client_setup();
}

void loop() {
  if (!mqtt.connected()) {
    mqtt_reconnect();
  }
  mqtt.loop();
  //yield();
  long now = millis();
  if (now - lastTime > interval) {
    lastTime = now;
    client_doit1();
    //client_doit2();
  }
}

```
